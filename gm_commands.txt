Private Sub HandleGMCommands(ByVal Userindex As Integer)
    '***************************************************
    'Author: Unknown
    'Last Modification: -
    '
    '***************************************************

    On Error GoTo errHandler

    Dim Command As Byte

    With UserList(Userindex)
        Call .incomingData.ReadByte
    
        Command = .incomingData.PeekByte
    
        Select Case Command

            Case eGMCommands.GMMessage                '/GMSG
                Call HandleGMMessage(Userindex)
        
            Case eGMCommands.showName                '/SHOWNAME
                Call HandleShowName(Userindex)
        
            Case eGMCommands.OnlineRoyalArmy
                Call HandleOnlineRoyalArmy(Userindex)
        
            Case eGMCommands.OnlineChaosLegion       '/ONLINECAOS
                Call HandleOnlineChaosLegion(Userindex)
        
            Case eGMCommands.GoNearby                '/IRCERCA
                Call HandleGoNearby(Userindex)
        
            Case eGMCommands.comment                 '/REM
                Call HandleComment(Userindex)
        
            Case eGMCommands.serverTime              '/HORA
                Call HandleServerTime(Userindex)
        
            Case eGMCommands.Where                   '/DONDE
                Call HandleWhere(Userindex)
        
            Case eGMCommands.CreaturesInMap          '/NENE
                Call HandleCreaturesInMap(Userindex)
        
            Case eGMCommands.WarpMeToTarget          '/TELEPLOC
                Call HandleWarpMeToTarget(Userindex)
        
            Case eGMCommands.WarpChar                '/TELEP
                Call HandleWarpChar(Userindex)
        
            Case eGMCommands.Silence                 '/SILENCIAR
                Call HandleSilence(Userindex)
        
            Case eGMCommands.SOSShowList             '/SHOW SOS
                Call HandleSOSShowList(Userindex)
            
            Case eGMCommands.SOSRemove               'SOSDONE
                Call HandleSOSRemove(Userindex)
        
            Case eGMCommands.GoToChar                '/IRA
                Call HandleGoToChar(Userindex)
        
            Case eGMCommands.invisible               '/INVISIBLE
                Call HandleInvisible(Userindex)
        
            Case eGMCommands.GMPanel                 '/PANELGM
                Call HandleGMPanel(Userindex)
        
            Case eGMCommands.RequestUserList         'LISTUSU
                Call HandleRequestUserList(Userindex)
        
            Case eGMCommands.Working                 '/TRABAJANDO
                Call HandleWorking(Userindex)
        
            Case eGMCommands.Hiding                  '/OCULTANDO
                Call HandleHiding(Userindex)
        
            Case eGMCommands.Jail                    '/CARCEL
                Call HandleJail(Userindex)
        
            Case eGMCommands.KillNPC                 '/RMATA
                Call HandleKillNPC(Userindex)
        
            Case eGMCommands.WarnUser                '/ADVERTENCIA
                Call HandleWarnUser(Userindex)
        
            Case eGMCommands.EditChar                '/MOD
                Call HandleEditChar(Userindex)
        
            Case eGMCommands.RequestCharInfo         '/INFO
                Call HandleRequestCharInfo(Userindex)
        
            Case eGMCommands.RequestCharStats        '/STAT
                Call HandleRequestCharStats(Userindex)
        
            Case eGMCommands.RequestCharGold         '/BAL
                Call HandleRequestCharGold(Userindex)
        
            Case eGMCommands.RequestCharInventory    '/INV
                Call HandleRequestCharInventory(Userindex)
        
            Case eGMCommands.RequestCharBank         '/BOV
                Call HandleRequestCharBank(Userindex)
        
            Case eGMCommands.RequestCharSkills       '/SKILLS
                Call HandleRequestCharSkills(Userindex)
        
            Case eGMCommands.ReviveChar              '/REVIVIR
                Call HandleReviveChar(Userindex)
        
            Case eGMCommands.OnlineGM                '/ONLINEGM
                Call HandleOnlineGM(Userindex)
        
            Case eGMCommands.OnlineMap               '/ONLINEMAP
                Call HandleOnlineMap(Userindex)
        
            Case eGMCommands.Forgive                 '/PERDON
                Call HandleForgive(Userindex)
        
            Case eGMCommands.Kick                    '/ECHAR
                Call HandleKick(Userindex)
        
            Case eGMCommands.Execute                 '/EJECUTAR
                Call HandleExecute(Userindex)
        
            Case eGMCommands.BanChar                 '/BAN
                Call HandleBanChar(Userindex)
        
            Case eGMCommands.UnbanChar               '/UNBAN
                Call HandleUnbanChar(Userindex)
        
            Case eGMCommands.NPCFollow               '/SEGUIR
                Call HandleNPCFollow(Userindex)
        
            Case eGMCommands.SummonChar              '/SUM
                Call HandleSummonChar(Userindex)
        
            Case eGMCommands.SpawnListRequest        '/CC
                Call HandleSpawnListRequest(Userindex)
        
            Case eGMCommands.SpawnCreature           'SPA
                Call HandleSpawnCreature(Userindex)
        
            Case eGMCommands.ResetNPCInventory       '/RESETINV
                Call HandleResetNPCInventory(Userindex)
        
            Case eGMCommands.ServerMessage           '/RMSG
                Call HandleServerMessage(Userindex)
        
            Case eGMCommands.MapMessage              '/MAPMSG
                Call HandleMapMessage(Userindex)
            
            Case eGMCommands.NickToIP                '/NICK2IP
                Call HandleNickToIP(Userindex)
        
            Case eGMCommands.IPToNick                '/IP2NICK
                Call HandleIPToNick(Userindex)
        
            Case eGMCommands.GuildOnlineMembers      '/ONCLAN
                Call HandleGuildOnlineMembers(Userindex)
        
            Case eGMCommands.TeleportCreate          '/CT
                Call HandleTeleportCreate(Userindex)
        
            Case eGMCommands.TeleportDestroy         '/DT
                Call HandleTeleportDestroy(Userindex)
        
            Case eGMCommands.RainToggle              '/LLUVIA
                Call HandleRainToggle(Userindex)
        
            Case eGMCommands.SetCharDescription      '/SETDESC
                Call HandleSetCharDescription(Userindex)

            Case eGMCommands.ForceMP3ToMap          '/FORCEMP3MAP
                Call HanldeForceMP3ToMap(Userindex)
        
            Case eGMCommands.ForceMIDIToMap          '/FORCEMIDIMAP
                Call HanldeForceMIDIToMap(Userindex)
        
            Case eGMCommands.ForceWAVEToMap          '/FORCEWAVMAP
                Call HandleForceWAVEToMap(Userindex)
        
            Case eGMCommands.RoyalArmyMessage        '/REALMSG
                Call HandleRoyalArmyMessage(Userindex)
        
            Case eGMCommands.ChaosLegionMessage      '/CAOSMSG
                Call HandleChaosLegionMessage(Userindex)
        
            Case eGMCommands.CitizenMessage          '/CIUMSG
                Call HandleCitizenMessage(Userindex)
        
            Case eGMCommands.CriminalMessage         '/CRIMSG
                Call HandleCriminalMessage(Userindex)
        
            Case eGMCommands.TalkAsNPC               '/TALKAS
                Call HandleTalkAsNPC(Userindex)
        
            Case eGMCommands.DestroyAllItemsInArea   '/MASSDEST
                Call HandleDestroyAllItemsInArea(Userindex)
        
            Case eGMCommands.AcceptRoyalCouncilMember '/ACEPTCONSE
                Call HandleAcceptRoyalCouncilMember(Userindex)
        
            Case eGMCommands.AcceptChaosCouncilMember '/ACEPTCONSECAOS
                Call HandleAcceptChaosCouncilMember(Userindex)
        
            Case eGMCommands.ItemsInTheFloor         '/PISO
                Call HandleItemsInTheFloor(Userindex)
        
            Case eGMCommands.MakeDumb                '/ESTUPIDO
                Call HandleMakeDumb(Userindex)
        
            Case eGMCommands.MakeDumbNoMore          '/NOESTUPIDO
                Call HandleMakeDumbNoMore(Userindex)
        
            Case eGMCommands.DumpIPTables            '/DUMPSECURITY
                Call HandleDumpIPTables(Userindex)
        
            Case eGMCommands.CouncilKick             '/KICKCONSE
                Call HandleCouncilKick(Userindex)
        
            Case eGMCommands.SetTrigger              '/TRIGGER
                Call HandleSetTrigger(Userindex)
        
            Case eGMCommands.AskTrigger              '/TRIGGER with no args
                Call HandleAskTrigger(Userindex)
        
            Case eGMCommands.BannedIPList            '/BANIPLIST
                Call HandleBannedIPList(Userindex)
        
            Case eGMCommands.BannedIPReload          '/BANIPRELOAD
                Call HandleBannedIPReload(Userindex)
        
            Case eGMCommands.GuildMemberList         '/MIEMBROSCLAN
                Call HandleGuildMemberList(Userindex)
        
            Case eGMCommands.GuildBan                '/BANCLAN
                Call HandleGuildBan(Userindex)
        
            Case eGMCommands.BanIP                   '/BANIP
                Call HandleBanIP(Userindex)
        
            Case eGMCommands.UnbanIP                 '/UNBANIP
                Call HandleUnbanIP(Userindex)
        
            Case eGMCommands.CreateItem              '/CI
                Call HandleCreateItem(Userindex)
        
            Case eGMCommands.DestroyItems            '/DEST
                Call HandleDestroyItems(Userindex)
        
            Case eGMCommands.ChaosLegionKick         '/NOCAOS
                Call HandleChaosLegionKick(Userindex)
        
            Case eGMCommands.RoyalArmyKick           '/NOREAL
                Call HandleRoyalArmyKick(Userindex)

            Case eGMCommands.ForceMP3All            '/FORCEMP3
                Call HandleForceMP3All(Userindex)
        
            Case eGMCommands.ForceMIDIAll            '/FORCEMIDI
                Call HandleForceMIDIAll(Userindex)
        
            Case eGMCommands.ForceWAVEAll            '/FORCEWAV
                Call HandleForceWAVEAll(Userindex)
        
            Case eGMCommands.RemovePunishment        '/BORRARPENA
                Call HandleRemovePunishment(Userindex)
        
            Case eGMCommands.TileBlockedToggle       '/BLOQ
                Call HandleTileBlockedToggle(Userindex)
        
            Case eGMCommands.KillNPCNoRespawn        '/MATA
                Call HandleKillNPCNoRespawn(Userindex)
        
            Case eGMCommands.KillAllNearbyNPCs       '/MASSKILL
                Call HandleKillAllNearbyNPCs(Userindex)
        
            Case eGMCommands.LastIP                  '/LASTIP
                Call HandleLastIP(Userindex)
        
            Case eGMCommands.ChangeMOTD              '/MOTDCAMBIA
                Call HandleChangeMOTD(Userindex)
        
            Case eGMCommands.SetMOTD                 'ZMOTD
                Call HandleSetMOTD(Userindex)
        
            Case eGMCommands.SystemMessage           '/SMSG
                Call HandleSystemMessage(Userindex)
        
            Case eGMCommands.CreateNPC               '/ACC y /RACC
                Call HandleCreateNPC(Userindex)
        
            Case eGMCommands.ImperialArmour          '/AI1 - 4
                Call HandleImperialArmour(Userindex)
        
            Case eGMCommands.ChaosArmour             '/AC1 - 4
                Call HandleChaosArmour(Userindex)
        
            Case eGMCommands.NavigateToggle          '/NAVE
                Call HandleNavigateToggle(Userindex)
        
            Case eGMCommands.ServerOpenToUsersToggle '/HABILITAR
                Call HandleServerOpenToUsersToggle(Userindex)
        
            Case eGMCommands.TurnOffServer           '/APAGAR
                Call HandleTurnOffServer(Userindex)
        
            Case eGMCommands.TurnCriminal            '/CONDEN
                Call HandleTurnCriminal(Userindex)
        
            Case eGMCommands.ResetFactions           '/RAJAR
                Call HandleResetFactions(Userindex)
        
            Case eGMCommands.RemoveCharFromGuild     '/RAJARCLAN
                Call HandleRemoveCharFromGuild(Userindex)
        
            Case eGMCommands.RequestCharMail         '/LASTEMAIL
                Call HandleRequestCharMail(Userindex)
        
            Case eGMCommands.AlterPassword           '/APASS
                Call HandleAlterPassword(Userindex)
        
            Case eGMCommands.AlterMail               '/AEMAIL
                Call HandleAlterMail(Userindex)
        
            Case eGMCommands.AlterName               '/ANAME
                Call HandleAlterName(Userindex)
        
            Case Declaraciones.eGMCommands.DoBackUp               '/DOBACKUP
                Call HandleDoBackUp(Userindex)
        
            Case eGMCommands.ShowGuildMessages       '/SHOWCMSG
                Call HandleShowGuildMessages(Userindex)
        
            Case eGMCommands.SaveMap                 '/GUARDAMAPA
                Call HandleSaveMap(Userindex)
        
            Case eGMCommands.ChangeMapInfoPK         '/MODMAPINFO PK
                Call HandleChangeMapInfoPK(Userindex)
            
            Case eGMCommands.ChangeMapInfoBackup     '/MODMAPINFO BACKUP
                Call HandleChangeMapInfoBackup(Userindex)
        
            Case eGMCommands.ChangeMapInfoRestricted '/MODMAPINFO RESTRINGIR
                Call HandleChangeMapInfoRestricted(Userindex)
        
            Case eGMCommands.ChangeMapInfoNoMagic    '/MODMAPINFO MAGIASINEFECTO
                Call HandleChangeMapInfoNoMagic(Userindex)
        
            Case eGMCommands.ChangeMapInfoNoInvi     '/MODMAPINFO INVISINEFECTO
                Call HandleChangeMapInfoNoInvi(Userindex)
        
            Case eGMCommands.ChangeMapInfoNoResu     '/MODMAPINFO RESUSINEFECTO
                Call HandleChangeMapInfoNoResu(Userindex)
        
            Case eGMCommands.ChangeMapInfoLand       '/MODMAPINFO TERRENO
                Call HandleChangeMapInfoLand(Userindex)
        
            Case eGMCommands.ChangeMapInfoZone       '/MODMAPINFO ZONA
                Call HandleChangeMapInfoZone(Userindex)
        
            Case eGMCommands.ChangeMapInfoStealNpc   '/MODMAPINFO ROBONPC
                Call HandleChangeMapInfoStealNpc(Userindex)
            
            Case eGMCommands.ChangeMapInfoNoOcultar  '/MODMAPINFO OCULTARSINEFECTO
                Call HandleChangeMapInfoNoOcultar(Userindex)
            
            Case eGMCommands.ChangeMapInfoNoInvocar  '/MODMAPINFO INVOCARSINEFECTO
                Call HandleChangeMapInfoNoInvocar(Userindex)
            
            Case eGMCommands.SaveChars               '/GRABAR
                Call HandleSaveChars(Userindex)
        
            Case eGMCommands.CleanSOS                '/BORRAR SOS
                Call HandleCleanSOS(Userindex)
        
            Case eGMCommands.ShowServerForm          '/SHOW INT
                Call HandleShowServerForm(Userindex)
        
            Case eGMCommands.night                   '/NOCHE
                Call HandleNight(Userindex)
        
            Case eGMCommands.KickAllChars            '/ECHARTODOSPJS
                Call HandleKickAllChars(Userindex)
        
            Case eGMCommands.ReloadNPCs              '/RELOADNPCS
                Call HandleReloadNPCs(Userindex)
        
            Case eGMCommands.ReloadServerIni         '/RELOADSINI
                Call HandleReloadServerIni(Userindex)
        
            Case eGMCommands.ReloadSpells            '/RELOADHECHIZOS
                Call HandleReloadSpells(Userindex)
        
            Case eGMCommands.ReloadObjects           '/RELOADOBJ
                Call HandleReloadObjects(Userindex)
        
            Case eGMCommands.Restart                 '/REINICIAR
                Call HandleRestart(Userindex)
        
            Case eGMCommands.ResetAutoUpdate         '/AUTOUPDATE
                Call HandleResetAutoUpdate(Userindex)
        
            Case eGMCommands.ChatColor               '/CHATCOLOR
                Call HandleChatColor(Userindex)
        
            Case eGMCommands.Ignored                 '/IGNORADO
                Call HandleIgnored(Userindex)
        
            Case eGMCommands.CheckSlot               '/SLOT
                Call HandleCheckSlot(Userindex)
        
            Case eGMCommands.SetIniVar               '/SETINIVAR LLAVE CLAVE VALOR
                Call HandleSetIniVar(Userindex)
            
            Case eGMCommands.CreatePretorianClan     '/CREARPRETORIANOS
                Call HandleCreatePretorianClan(Userindex)
         
            Case eGMCommands.RemovePretorianClan     '/ELIMINARPRETORIANOS
                Call HandleDeletePretorianClan(Userindex)
                
            Case eGMCommands.EnableDenounces         '/DENUNCIAS
                Call HandleEnableDenounces(Userindex)
            
            Case eGMCommands.ShowDenouncesList       '/SHOW DENUNCIAS
                Call HandleShowDenouncesList(Userindex)
        
            Case eGMCommands.SetDialog               '/SETDIALOG
                Call HandleSetDialog(Userindex)
            
            Case eGMCommands.Impersonate             '/IMPERSONAR
                Call HandleImpersonate(Userindex)
            
            Case eGMCommands.Imitate                 '/MIMETIZAR
                Call HandleImitate(Userindex)
            
            Case eGMCommands.RecordAdd
                Call HandleRecordAdd(Userindex)
            
            Case eGMCommands.RecordAddObs
                Call HandleRecordAddObs(Userindex)
            
            Case eGMCommands.RecordRemove
                Call HandleRecordRemove(Userindex)
            
            Case eGMCommands.RecordListRequest
                Call HandleRecordListRequest(Userindex)
            
            Case eGMCommands.RecordDetailsRequest
                Call HandleRecordDetailsRequest(Userindex)
            
            Case eGMCommands.ExitDestroy
                Call HandleExitDestroy(Userindex)

            Case eGMCommands.ToggleCentinelActivated            '/CENTINELAACTIVADO
                Call HandleToggleCentinelActivated(Userindex)
        
            Case eGMCommands.SearchNpc                          '/BUSCAR
                Call HandleSearchNpc(Userindex)
           
            Case eGMCommands.SearchObj                          '/BUSCAR
                Call HandleSearchObj(Userindex)
                                           
            Case eGMCommands.LimpiarMundo                       '/LIMPIARMUNDO
                Call HandleLimpiarMundo(Userindex)
                                           
        End Select

    End With

    Exit Sub

errHandler:
    Call LogError("Error en GmCommands. Error: " & Err.Number & " - " & Err.description & ". Paquete: " & Command)

End Sub

Private Sub HandleGMMessage(ByVal Userindex As Integer)

    '***************************************************
    'Author: Juan Martin Sotuyo Dodero (Maraxus)
    'Last Modification: 01/08/07
    'Last Modification by: (liquid)
    '***************************************************
    If UserList(Userindex).incomingData.Length < 3 Then
        Err.Raise UserList(Userindex).incomingData.NotEnoughDataErrCode
        Exit Sub

    End If
    
    On Error GoTo errHandler

    With UserList(Userindex)

        'This packet contains strings, make a copy of the data to prevent losses if it's not complete yet...
        Dim buffer As clsByteQueue
        Set buffer = New clsByteQueue

        Call buffer.CopyBuffer(.incomingData)
        
        'Remove packet ID
        Call buffer.ReadByte
        
        Dim Message As String
        
        Message = buffer.ReadASCIIString()
        
        If Not .flags.Privilegios And PlayerType.User Then
            Call LogGM(.Name, "Mensaje a Gms:" & Message)
        
            If LenB(Message) <> 0 Then
                'Analize chat...
                Call Statistics.ParseChat(Message)
            
                Call SendData(SendTarget.ToAdmins, 0, PrepareMessageConsoleMsg(.Name & "> " & Message, FontTypeNames.FONTTYPE_GMMSG))

            End If

        End If
        
        'If we got here then packet is complete, copy data back to original queue
        Call .incomingData.CopyBuffer(buffer)

    End With
    
errHandler:

    Dim Error As Long

    Error = Err.Number

    On Error GoTo 0
    
    'Destroy auxiliar buffer
    Set buffer = Nothing
    
    If Error <> 0 Then Err.Raise Error

End Sub


Private Sub HandleShowName(ByVal Userindex As Integer)

    '***************************************************
    'Author: Juan Martin Sotuyo Dodero (Maraxus)
    'Last Modification: 05/17/06
    '
    '***************************************************
    With UserList(Userindex)
        'Remove packet ID
        Call .incomingData.ReadByte
        
        If .flags.Privilegios And (PlayerType.Dios Or PlayerType.Admin Or PlayerType.RoleMaster) Then
            .showName = Not .showName 'Show / Hide the name
            
            Call RefreshCharStatus(Userindex)

        End If

    End With

End Sub

Private Sub HandleWarpChar(ByVal Userindex As Integer)

    '***************************************************
    'Author: Juan Martin Sotuyo Dodero (Maraxus)
    'Last Modification: 11/08/2019
    '26/03/2009: ZaMa - Chequeo que no se teletransporte a un tile donde haya un char o npc.
    '11/08/2019: Jopi - No registramos en los logs si te teletransportas a vos mismo.
    '***************************************************
    If UserList(Userindex).incomingData.Length < 7 Then
        Err.Raise UserList(Userindex).incomingData.NotEnoughDataErrCode
        Exit Sub

    End If
    
    On Error GoTo errHandler

    With UserList(Userindex)

        'This packet contains strings, make a copy of the data to prevent losses if it's not complete yet...
        Dim buffer As clsByteQueue
        Set buffer = New clsByteQueue

        Call buffer.CopyBuffer(.incomingData)
        
        'Remove packet ID
        Call buffer.ReadByte
        
        Dim UserName As String

        Dim Map      As Integer

        Dim X        As Integer

        Dim Y        As Integer

        Dim tUser    As Integer
        
        UserName = buffer.ReadASCIIString()
        Map = buffer.ReadInteger()
        X = buffer.ReadByte()
        Y = buffer.ReadByte()
        
        If Not .flags.Privilegios And PlayerType.User Then
            If MapaValido(Map) And LenB(UserName) <> 0 Then
                If UCase$(UserName) <> "YO" Then
                    If Not .flags.Privilegios And PlayerType.Consejero Then
                        tUser = NameIndex(UserName)

                    End If

                Else
                    tUser = Userindex

                End If
            
                If tUser <= 0 Then
                    If Not (EsDios(UserName) Or EsAdmin(UserName)) Then
                        Call WriteConsoleMsg(Userindex, "Usuario offline.", FontTypeNames.FONTTYPE_INFO)
                    Else
                        Call WriteConsoleMsg(Userindex, "No puedes transportar dioses o admins.", FontTypeNames.FONTTYPE_INFO)

                    End If
                    
                ElseIf Not ((UserList(tUser).flags.Privilegios And PlayerType.Dios) <> 0 Or (UserList(tUser).flags.Privilegios And PlayerType.Admin) <> 0) Or tUser = Userindex Then
                            
                    If InMapBounds(Map, X, Y) Then
                        Call FindLegalPos(tUser, Map, X, Y)
                        Call WarpUserChar(tUser, Map, X, Y, True, True)
                        
                        ' Agrego esto para no llenar consola de mensajes al hacer SHIFT + CLICK DERECHO
                        If Userindex <> tUser Then
                            Call WriteConsoleMsg(Userindex, UserList(tUser).Name & " transportado.", FontTypeNames.FONTTYPE_INFO)
                            Call LogGM(.Name, "Transporto a " & UserList(tUser).Name & " hacia " & "Mapa" & Map & " X:" & X & " Y:" & Y)

                        End If
                    End If

                Else
                    Call WriteConsoleMsg(Userindex, "No puedes transportar dioses o admins.", FontTypeNames.FONTTYPE_INFO)

                End If

            End If

        End If
        
        'If we got here then packet is complete, copy data back to original queue
        Call .incomingData.CopyBuffer(buffer)

    End With
    
errHandler:

    Dim Error As Long

    Error = Err.Number

    On Error GoTo 0
    
    'Destroy auxiliar buffer
    Set buffer = Nothing
    
    If Error <> 0 Then Err.Raise Error

End Sub

Private Sub HandleGoToChar(ByVal Userindex As Integer)

    '***************************************************
    'Author: Juan Martin Sotuyo Dodero (Maraxus)
    'Last Modification: 26/03/2009
    '26/03/2009: ZaMa -  Chequeo que no se teletransporte a un tile donde haya un char o npc.
    '***************************************************
    If UserList(Userindex).incomingData.Length < 3 Then
        Err.Raise UserList(Userindex).incomingData.NotEnoughDataErrCode
        Exit Sub

    End If
    
    On Error GoTo errHandler

    With UserList(Userindex)

        'This packet contains strings, make a copy of the data to prevent losses if it's not complete yet...
        Dim buffer As clsByteQueue
        Set buffer = New clsByteQueue

        Call buffer.CopyBuffer(.incomingData)
        
        'Remove packet ID
        Call buffer.ReadByte
        
        Dim UserName As String

        Dim tUser    As Integer

        Dim X        As Integer

        Dim Y        As Integer
        
        UserName = buffer.ReadASCIIString()
        tUser = NameIndex(UserName)
        
        If .flags.Privilegios And (PlayerType.Dios Or PlayerType.Admin Or PlayerType.SemiDios Or PlayerType.Consejero) Then

            'Si es dios o Admins no podemos salvo que nosotros tambien lo seamos
            If Not (EsDios(UserName) Or EsAdmin(UserName)) Or (.flags.Privilegios And (PlayerType.Dios Or PlayerType.Admin)) <> 0 Then
                If tUser <= 0 Then
                    Call WriteConsoleMsg(Userindex, "Usuario offline.", FontTypeNames.FONTTYPE_INFO)
                Else
                    X = UserList(tUser).Pos.X
                    Y = UserList(tUser).Pos.Y + 1
                    Call FindLegalPos(Userindex, UserList(tUser).Pos.Map, X, Y)
                    
                    Call WarpUserChar(Userindex, UserList(tUser).Pos.Map, X, Y, True)
                    
                    If .flags.AdminInvisible = 0 Then
                        Call WriteConsoleMsg(tUser, .Name & " se ha trasportado hacia donde te encuentras.", FontTypeNames.FONTTYPE_INFO)

                    End If
                    
                    Call LogGM(.Name, "/IRA " & UserName & " Mapa:" & UserList(tUser).Pos.Map & " X:" & UserList(tUser).Pos.X & " Y:" & UserList(tUser).Pos.Y)

                End If

            End If

        End If
        
        'If we got here then packet is complete, copy data back to original queue
        Call .incomingData.CopyBuffer(buffer)

    End With
    
errHandler:

    Dim Error As Long

    Error = Err.Number

    On Error GoTo 0
    
    'Destroy auxiliar buffer
    Set buffer = Nothing
    
    If Error <> 0 Then Err.Raise Error

End Sub

Private Sub HandleExecute(ByVal Userindex As Integer)

    '***************************************************
    'Author: Nicolas Matias Gonzalez (NIGO)
    'Last Modification: 07/06/2010
    '07/06/2010: ZaMa - Ahora no se puede usar para saber si hay dioses/admins online.
    '***************************************************
    If UserList(Userindex).incomingData.Length < 3 Then
        Err.Raise UserList(Userindex).incomingData.NotEnoughDataErrCode
        Exit Sub

    End If
    
    On Error GoTo errHandler

    With UserList(Userindex)

        'This packet contains strings, make a copy of the data to prevent losses if it's not complete yet...
        Dim buffer As clsByteQueue
        Set buffer = New clsByteQueue

        Call buffer.CopyBuffer(.incomingData)
        
        'Remove packet ID
        Call buffer.ReadByte
        
        Dim UserName As String

        Dim tUser    As Integer
        
        UserName = buffer.ReadASCIIString()

        If (Not .flags.Privilegios And PlayerType.RoleMaster) <> 0 And (.flags.Privilegios And (PlayerType.Admin Or PlayerType.Dios Or PlayerType.SemiDios)) <> 0 Then
            tUser = NameIndex(UserName)
            
            If tUser > 0 Then
                If Not UserList(tUser).flags.Privilegios And PlayerType.User Then
                    Call WriteConsoleMsg(Userindex, "Estas loco?? Como vas a pinatear un gm?? :@", FontTypeNames.FONTTYPE_INFO)
                Else
                    Call UserDie(tUser)
                    Call SendData(SendTarget.ToAll, 0, PrepareMessageConsoleMsg(.Name & " ha ejecutado a " & UserName & ".", FontTypeNames.FONTTYPE_EJECUCION))
                    Call LogGM(.Name, " ejecuto a " & UserName)

                End If

            Else

                If Not (EsDios(UserName) Or EsAdmin(UserName)) Then
                    Call WriteConsoleMsg(Userindex, "No esta online.", FontTypeNames.FONTTYPE_INFO)
                Else
                    Call WriteConsoleMsg(Userindex, "Estas loco?? Como vas a pinatear un gm?? :@", FontTypeNames.FONTTYPE_INFO)

                End If

            End If

        End If
        
        'If we got here then packet is complete, copy data back to original queue
        Call .incomingData.CopyBuffer(buffer)

    End With

errHandler:

    Dim Error As Long

    Error = Err.Number

    On Error GoTo 0
    
    'Destroy auxiliar buffer
    Set buffer = Nothing
    
    If Error <> 0 Then Err.Raise Error

End Sub

Private Sub HandleSummonChar(ByVal Userindex As Integer)

    '***************************************************
    'Author: Nicolas Matias Gonzalez (NIGO)
    'Last Modification: 26/03/2009
    '26/03/2009: ZaMa - Chequeo que no se teletransporte donde haya un char o npc
    '***************************************************
    If UserList(Userindex).incomingData.Length < 3 Then
        Err.Raise UserList(Userindex).incomingData.NotEnoughDataErrCode
        Exit Sub

    End If
    
    On Error GoTo errHandler

    With UserList(Userindex)

        'This packet contains strings, make a copy of the data to prevent losses if it's not complete yet...
        Dim buffer As clsByteQueue
        Set buffer = New clsByteQueue

        Call buffer.CopyBuffer(.incomingData)
        
        'Remove packet ID
        Call buffer.ReadByte
        
        Dim UserName As String

        Dim tUser    As Integer

        Dim X        As Integer

        Dim Y        As Integer
        
        UserName = buffer.ReadASCIIString()
        
        If (.flags.Privilegios And (PlayerType.Admin Or PlayerType.Dios Or PlayerType.SemiDios)) Then
            tUser = NameIndex(UserName)
            
            If tUser <= 0 Then
                If EsDios(UserName) Or EsAdmin(UserName) Then
                    Call WriteConsoleMsg(Userindex, "No puedes invocar a dioses y admins.", FontTypeNames.FONTTYPE_INFO)
                Else
                    Call WriteConsoleMsg(Userindex, "El jugador no esta online.", FontTypeNames.FONTTYPE_INFO)

                End If
                
            Else

                If (.flags.Privilegios And (PlayerType.Dios Or PlayerType.Admin)) <> 0 Or (UserList(tUser).flags.Privilegios And (PlayerType.Consejero Or PlayerType.User)) <> 0 Then
                    Call WriteConsoleMsg(tUser, .Name & " te ha trasportado.", FontTypeNames.FONTTYPE_INFO)
                    X = .Pos.X
                    Y = .Pos.Y + 1
                    Call FindLegalPos(tUser, .Pos.Map, X, Y)
                    Call WarpUserChar(tUser, .Pos.Map, X, Y, True, True)
                    Call LogGM(.Name, "/SUM " & UserName & " Map:" & .Pos.Map & " X:" & .Pos.X & " Y:" & .Pos.Y)
                Else
                    Call WriteConsoleMsg(Userindex, "No puedes invocar a dioses y admins.", FontTypeNames.FONTTYPE_INFO)

                End If

            End If

        End If
        
        'If we got here then packet is complete, copy data back to original queue
        Call .incomingData.CopyBuffer(buffer)

    End With

errHandler:

    Dim Error As Long

    Error = Err.Number

    On Error GoTo 0
    
    'Destroy auxiliar buffer
    Set buffer = Nothing
    
    If Error <> 0 Then Err.Raise Error

End Sub